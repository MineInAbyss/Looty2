plugins {
    id 'java'
    id 'idea'
    id 'com.google.protobuf' version '0.8.11'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'com.derongan.generation.looty.ComponentGeneration'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.15.1-R0.1-SNAPSHOT'
    compileOnly 'com.google.guava:guava:21.0'
    implementation 'com.google.auto.value:auto-value-annotations:1.6.3'
    implementation 'com.google.dagger:dagger:2.+'
    implementation 'com.badlogicgames.ashley:ashley:1.7.3'
    implementation 'javax.inject:javax.inject:1'
    implementation('com.google.protobuf:protobuf-java:3.11.1') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation('com.google.protobuf:protobuf-java-util:3.11.1') {
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation 'org.jetbrains:annotations:17.0.0'
    testImplementation 'org.spigotmc:spigot-api:1.15.1-R0.1-SNAPSHOT'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.25.1'
    testImplementation "com.google.truth:truth:0.34"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.+'
    annotationProcessor "com.google.auto.value:auto-value:1.6.3"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    minimize()
}

task copyJar(type: Copy) {
    from shadowJar // here it automatically reads jar file produced from jar task
    into '/srv/craftbukkit/plugins/'
}

build.dependsOn copyJar